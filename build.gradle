buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'nu.studer.plugindev' version '1.0.3'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.marketcetera.fork:commons-csv:2.4.1'
    compile 'org.jsoup:jsoup:1.8.1'
    testCompile 'junit:junit:4.11'
}

group = GROUP
version = VERSION_NAME

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME

            pom.project {
                name POM_NAME
                packaging POM_PACKAGING
                description POM_DESCRIPTION
                url POM_URL

                scm {
                    url POM_SCM_URL
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_DEV_CONNECTION
                }

                licenses {
                    license {
                        name POM_LICENCE_NAME
                        url POM_LICENCE_URL
                        distribution POM_LICENCE_DIST
                    }
                }

                developers {
                    developer {
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_NAME
                    }
                }
            }
        }
    }
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocsJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

plugindev {
    pluginImplementationClass 'pl.droidsonroids.gradle.localization.LocalizationPlugin'
    pluginDescription POM_DESCRIPTION
    pluginLicenses POM_LICENCE_NAME
    pluginTags 'gradle', 'plugin', 'localization'
    authorId POM_DEVELOPER_ID
    authorName POM_DEVELOPER_NAME
    authorEmail POM_EMAIL
    projectUrl POM_URL
    projectInceptionYear POM_INCEPTION_YEAR
    done()
}

bintray {
    user = POM_DEVELOPER_ID
    key = BINTRAY_API_KEY
    pkg.repo = 'maven'
}